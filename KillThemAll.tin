#READ reset.tin
#ACTION {^【故事】%W} {
    #regex {%1} {的基本會員！}
    {#nop}
    {
        #var warStatus {1};
        #delay {clearWarStatus} {#unvar warStatus} {900};
    }
}

#ACTION {^以下為本次天朝勇士的傷害排名：}
{
    #unvar warStatus;
}

#act {^{這裡沒有這個人。}$}
{
    #delay {AppendFightRoomIndex}
    {
        AppendFightRoomIndex;
    } {5};
}

#act {^{看起來\W+想殺死你﹗|你已經是和\W+性命相博了中.}}
{
	#untick {AppendFightRoomIndex};
}

#act {^{\W+緩緩地移動腳步﹐想要找出你的破綻。|\W+正盯著你的一舉一動﹐隨時準備發動攻勢。|\W+緩緩地移動腳步﹐想要找出你的破綻。|\W+慢慢地移動著腳步﹐伺機出手。|\W+目不轉睛地盯著你的動作﹐尋找進攻的最佳時機。}}
{
	#untick {AppendFightRoomIndex};
}

#act {^你的術士天眼法術失效了。$}
{
    #unvar lightStatus;
}

#act {^{你微一凝神﹐閉著眼睛念了幾句咒語﹐施展「術士天眼」法術。|你已經施展過術士天眼的法術了。}$}
{
    #var lightStatus 1;
}

#act {^你身上帶著下列這些東西(負重 %d\%)﹕}
{
	#var loading %1;
}

#alias {AppendFightRoomIndex}
{
    #math {fightRoomIndex} {$fightRoomIndex+1};
    #if {"$fightRoomList[$fightRoomIndex]"==""}
    {
		#var fightRoomIndex 1;
		#var warStatus {1};
        #delay {clearWarStatus} {#unvar warStatus} {300};
    };
	#foreach {1;2;3;4;5} {corpseNum}
	{	
		get all from corpse $corpseNum;
		get all from skeleton $corpseNum;
	};
	i;
	#delay {1}
	{
		#if {$loading>50}{
			StopTicker;
			#event {END OF RUN}
			{
				#unevent {END OF RUN};
                mark cake;
				mark ice;
				sell all;
			};
			#map run 183;
			#event {END OF RUN}
			{
                #unevent {END OF RUN};
				mark cake;
				mark ice;
				drop all;
			};
			SafeRoom;
			StartTicker;
		};
	}
}

#ALIAS {KillMob}
{
    #map get {roomvnum} {currentRoom};
    #if {"$currentRoom"=="$fightRoomList[$fightRoomIndex][1]"}
    {
        #foreach {$fightRoomList[$fightRoomIndex][2]} {mob} {kill $mob}
    };
    #else
    {
        #event {END OF RUN}
        {
            #unevent {END OF RUN};
			get silver;
            #foreach {$fightRoomList[$fightRoomIndex][2]} {mob} {kill $mob}
        };
        #map run {$fightRoomList[$fightRoomIndex][1]} {0.1};
    };
}

#ALIAS {Condition}
{
    #if {$addFood} {
        StopTicker;
        AddFood;
        StartTicker;
    };
    #elseif {$addWater}{
        StopTicker;
        AddWater;
        StartTicker;
    };
    #elseif {!&{lightStatus}}
    {
        Lighting;
    };
    #elseif {!&{warStatus}}
    {
		#if {$health[kee]<$health[keeMax] && $health[force]>0}
        {
            exert recover;
        };
        #elseif {$health[kee] >= $health[keeMax] && $health[keeLimit]==100}
        {
            KillMob;
        };
        #elseif {$health[force]==0}
        {
            Notice Start Cultivate;
            StopTicker;
            #class Cultivate read Cultivate.tin;
            StartCultivate;
        };
        #else {
            time;
        };
    };
    #elseif {$warStatus}
    {
        halt;halt;
        StopTicker;
        #class Cultivate read Cultivate.tin;
        StartCultivate;
    };
    #else {
        time
    }
}


#ALIAS {StopTicker}
{
    Notice Stop Ticker;
    #class {%*} kill;
    #UNTICKER {ConditionTicker};
}

#ALIAS {StartTicker}
{
    Notice Start Ticker;
    #TICKER {ConditionTicker} {
        hp;
        #delay {1} {Condition};
    } {10}
}

#function {CultivateInterrupt}
{
	#if {$health[force]>=$health[forceMax] && $health[kee]>=$health[keeMax] && !&{warStatus}}
	{
        #return 1;
	};
	#else {
        #return 0;
    };
}

#ALIAS {Home} { #map run {天涯坊大廳} 0.1 }
#var safeRoom {691}
#ALIAS {SafeRoom} { #map run {$safeRoom} 0.1 }

#unvar interrupt;
#unvar warStatus;
#unvar lightStatus;
#alias {Lighting} {cast aura};

#var atmanStatus 1;
#var forceStatus 1;
#var manaStatus 1;

#var fightRoomIndex 1;
#list {fightRoomList} {clear}
#foreach
{
    {{1}{94}{2}{bandit;bandit 2;bandit 3;leader}}
	{{1}{230}{2}{gangster;gangster 2;gangster 3}}
	{{1}{233}{2}{gangster;gangster 2}}
	{{1}{245}{2}{bandit;bandit 2;bandit 3}}
	{{1}{299}{2}{gangster;gangster 2}}
	{{1}{258}{2}{rogue;rogue 2;rogue 3;gangster}}
	{{1}{308}{2}{gangster;gangster 2}}
	{{1}{329}{2}{bandit;bandit 2;bandit 3}}
	{{1}{332}{2}{leader;leader 2;leader 3;leader 4}}
	{{1}{336}{2}{bandit;bandit 2;bandit 3}}
	{{1}{379}{2}{bandit;bandit 2;bandit 3;leader;leader 2}}
	{{1}{383}{2}{bandit;bandit 2;leader;leader 2}}
	{{1}{385}{2}{bandit;bandit 2;bandit 3}}
} {mob} {#list {fightRoomList} {add} {{$mob}}}