#READ reset.tin
#ACTION {^【故事】%W} {
    #regex {%1} {的基本會員！}
    {#nop}
    {
        #var warStatus {1};
        #delay {clearWarStatus} {#unvar warStatus} {900};
    }
}

#ACTION {^以下為本次天朝勇士的傷害排名：}
{
    #unvar warStatus;
}

#act {^陸得財是一個渾身髒兮兮的老丐﹐一副無精打采要死不活的樣子﹐可是$}
{
	#map get {roomvnum} {masterVnum};
}

#ALIAS {FightMob}
{
    #map get {roomvnum} {currentRoom};
    #if {"$currentRoom"=="$fightRoomList[$fightRoomIndex][1]"}
    {
        #foreach {$fightRoomList[$fightRoomIndex][2]} {mob} {fight $mob}
    };
    #else
    {
        #event {END OF RUN}
        {
            #unevent {END OF RUN};
            #foreach {$fightRoomList[$fightRoomIndex][2]} {mob} {fight $mob}
        };
        #map run {$fightRoomList[$fightRoomIndex][1]} {0.1};
    };
}

#ALIAS {LearnSkill}
{
    Notice FindMaster;
    #var {anchor} {1};
    #unvar masterVnum;
    #event {END OF RUN} {
        #math {anchor} {$anchor+1};
        l master;
        #delay {1}
        {
            #if {$masterPath[$anchor] && !&{masterVnum}}{
                #map run {$masterPath[$anchor]} {0.1};
            };
            #else {
                #unevent {END OF RUN};
                #if {&masterVnum}
                {
                    Notice Found Master;
                    learn $learnSkill from $master;
                };
                #else
                {
                    Notice Learn Pass;
                    #var learnPass {1};
                    #delay {300} {
                        #unvar learnPass;
                    };
                    StartTicker;
                };
            };
        };
    };
    #map run {$masterPath[$anchor]} {0.1};
}

#ALIAS {Condition}
{
    #if {$addFood} {
        StopTicker;
        AddFood;
        StartTicker;
    };
    #elseif {$addWater}{
        StopTicker;
        AddWater;
        StartTicker;
    };
    #elseif {!&{warStatus}}
    {
        #if {$potential >= 20 && !&{learnPass}}{
            Notice Start Learn;
            StopTicker;
            #class ModuleLearn read ModuleLearn.tin;
            LearnSkill;
        };
        #elseif {$health[sen]>=$health[senMax] && $fightStatus==0 && &studyItem}
        {
            Notice Start Study;
            StopTicker;
            #class ModuleStudy read ModuleStudy.tin;
            StudyItem;
        };
        #elseif {$health[kee] >= $health[keeMax] && $health[keeLimit]==100}
        {
            Notice Start Fight;
            #class ModuleFight read ModuleFight.tin;
            FightMob;
        };
        #elseif {$health[kee]<$health[keeMax] && $health[force]>0}
        {
            exert recover;
        };
        #elseif {$health[force]==0}
        {
            StopTicker;
            #class Cultivate read Cultivate.tin;
            StartCultivate;
        };
        #else {
            time;
        };
    };
    #elseif {$warStatus}
    {
        StopTicker;
        #class Cultivate read Cultivate.tin;
        StartCultivate;
    };
    #else {
        time
    }
}

#ALIAS {StopTicker}
{
    Notice Stop Ticker;
    #class {%*} kill;
    #UNTICKER {ConditionTicker};
}

#ALIAS {StartTicker}
{
    Notice Start Ticker;
    #TICKER {ConditionTicker} {
        hp;
        #delay {1} {Condition};
    } {10}
}

#function {InterruptCheck}
{
	#if {$potential>=80 || &{warStatus}}
	{
        #return 1;
	};
	#elseif {$health[sen]>=$health[senMax] && &{studyItem}}
    {
        #return 1;
    };
    #else {
        #return 0;
    };
}

#function {CultivateInterrupt}
{
    #if {&warStatus}
    {
        #return 0;
    };
    #elseif {$health[kee]>=$health[keeMax] && $health[force]>=$health[forceMax]}
    {
        #return 1;
    };
    #else {
        #return 0;
    };
}

#unvar warStatus;

#unvar learnPass;
#var learnSkill {force}
#var master {master}
#var masterPath {{1}{22}{2}{241}{3}{251}{4}{259}{5}{351}{6}{410}}
#var {anchor} {1}

#VARIABLE {fightStatus} {0};
#var fightRoomIndex 1;
#ALIAS {FightRoom} { #map run {$fightRoomList[$fightRoomIndex]} 0.1 }
#variable {fightRoomList}
{
  {1}{{1}{751}{2}{fuyun}}
  {2}{{1}{750}{2}{lou}}
  {3}{{1}{766}{2}{maysan}}
  {4}{{1}{760}{2}{ren}}
}

#var atmanStatus 1;
#var forceStatus 1;
#var manaStatus 1;

#var studyItem {book};

#var practiceSkill {staff}