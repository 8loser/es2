#READ reset.tin
#ACTION {^【故事】%W} {
    #regex {%1} {的基本會員！}
    {#nop}
    {
        #var warStatus {1};
        #delay {clearWarStatus} {#unvar warStatus} {900};
    }
}

#ACTION {^以下為本次天朝勇士的傷害排名：}
{
    #unvar warStatus;
}

#act {%W東方蠱(Master poison)$}
{
	#map get {roomvnum} {masterVnum};
}

#ALIAS {FightMob}
{
    #map get {roomvnum} {currentRoom};
    #if {"$currentRoom"=="$fightRoomList[$fightRoomIndex][1]"}
    {
        #foreach {$fightRoomList[$fightRoomIndex][2]} {mob} {fight $mob}
    };
    #else
    {
        #event {END OF RUN}
        {
            #unevent {END OF RUN};
            #foreach {$fightRoomList[$fightRoomIndex][2]} {mob} {fight $mob}
        };
        #map run {$fightRoomList[$fightRoomIndex][1]} {0.1};
    };
}

#ALIAS {LearnSkill}
{
    Notice FindMaster;
    #var {anchor} {1};
    #unvar masterVnum;

    #ticker {FindMasterTicker} {
        #if {$masterPath[$anchor] && !&{masterVnum}}{
            #map run {$masterPath[$anchor]} {0.1};
            #math {anchor} {$anchor+1};
        };
        #else {
            #unticker {FindMasterTicker};
            #if {&masterVnum}
            {
                Notice Found Master;
                #map run {$masterVnum} {0.1};
                #delay {1} {learn $learnSkill from $master};
            };
            #else
            {
                Notice Learn Pass;
                #var learnPass {1};
                #delay {300} {
                    #unvar learnPass;
                };
                StartTicker;
            };
        };
    } {1}
}

#ALIAS {Condition}
{
    #if {$addFood} {
        StopTicker;
        AddFood;
        StartTicker;
    };
    #elseif {$addWater}{
        StopTicker;
        AddWater;
        StartTicker;
    };
    #elseif {!&{warStatus}}
    {
        #if {$potential >= 20 && !&{learnPass}}{
            Notice Start Learn;
            StopTicker;
            #class ModuleLearn read ModuleLearn.tin;
            LearnSkill;
        };
        #elseif {$health[sen]>=$health[senMax] && $fightStatus==0 && &{studyItem}}
        {
            Notice Start Study;
            StopTicker;
            #class ModuleStudy read ModuleStudy.tin;
            StudyItem;
        };
        #elseif {$health[kee] >= $health[keeMax] && $health[keeLimit]==100}
        {
            Notice Start Fight;
            #class ModuleFight read ModuleFight.tin;
            FightMob;
        };
        #elseif {$health[kee]<$health[keeMax] && $health[force]>0}
        {
            exert recover;
        };
        #elseif {$health[force]==0}
        {
            StopTicker;
            #class Cultivate read Cultivate.tin;
            StartCultivate;
        };
        #else {
            time;
        };
    };
    #elseif {$warStatus}
    {
        StopTicker;
        #class Cultivate read Cultivate.tin;
        StartCultivate;
    };
    #else {
        time
    }
}

#ALIAS {StopTicker}
{
    Notice Stop Ticker;
    #class {%*} kill;
    #UNTICKER {ConditionTicker};
}

#ALIAS {StartTicker}
{
    Notice Start Ticker;
    #TICKER {ConditionTicker} {
        hp;
        #delay {1} {Condition};
    } {10}
}

#function {InterruptCheck}
{
	#if { ($potential>=20 && !&{learnPass} ) || &{warStatus}}
	{
        #return 1;
	};
	#elseif {$health[sen]>=$health[senMax] && &{studyItem}}
    {
        #return 1;
    };
    #else {
        #return 0;
    };
}

#function {CultivateInterrupt}
{
    #if {&warStatus}
    {
        #return 0;
    };
    #elseif {$health[kee]>=$health[keeMax] && $health[force]>=$health[forceMax]}
    {
        #return 1;
    };
    #elseif {$potential>=20 && !&{warStatus} && !&{learnPass}}
    {
        #return 1;
    };
    #elseif {$health[sen]>=$health[senMax] && $manaStatus==0 && &{studyItem}}
    {
        #return 1;
    };
    #else {
        #return 0;
    };
}

#unvar warStatus;

#unvar learnPass;
#var learnSkill {force}
#var master {master poison}
#var masterPath {{1}{246}{2}{247}{3}{248}{4}{249}{5}{251}{6}{260}{7}{261}{8}{263}{9}{270}{10}{271}{11}{275}{12}{276}}
#var {anchor} {1}

#VARIABLE {fightStatus} {0};
#var fightRoomIndex 1;
#list {fightRoomList} {clear}
#foreach
{
    {{1}{418}{2}{girl}}
    {{1}{318}{2}{yuan}}
    {{1}{552}{2}{guest}}
    {{1}{553}{2}{guest}}
} {mob} {#list {fightRoomList} {add} {{$mob}}}

#var atmanStatus 1;
#var forceStatus 1;
#var manaStatus 1;

#var studyItem {book}

#var practiceSkill {unarmed}
